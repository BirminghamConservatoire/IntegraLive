<?xml version="1.0" encoding="utf-8"?>
<!--
 * Integra Live graphical user interface
 *
 * Copyright (C) 2009 Birmingham City University
 *
 * This program is free software; you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation; either version 2 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA   02110-1301,
 * USA.
 -->

<IntegraView xmlns="components.views.*" 
			 xmlns:fx="http://ns.adobe.com/mxml/2009" 
			 xmlns:s="library://ns.adobe.com/flex/spark" 
			 xmlns:mx="library://ns.adobe.com/flex/mx"
			 xmlns:viewContainers="components.views.viewContainers.*"
				creationComplete="onCreationComplete()" 
				horizontalScrollPolicy="off"
				verticalScrollPolicy="off" xmlns:utils="components.utils.*">

	<states>
		<s:State name="ArrangeState"/>
		<s:State name="ModuleGraphState"/>
		<s:State name="LiveState"/>
	</states> 
	
	<mx:VBox width="100%" height="100%" paddingLeft="5" paddingRight="5" verticalGap="2" includeIn="ArrangeState">
		<mx:HBox width="100%" height="100%" horizontalGap="2">
			<viewContainers:ViewHolder view="{_arrangeView}" timelineMode="{TimelineMode.EDITABLE}" hasActiveButton="true"/>
			<viewContainers:ViewHolder view="{_blockLibrary}" changeWidthFromLeft="true"/>
		</mx:HBox>
		<mx:HBox id="_arrangeInfoContainer" width="100%" horizontalGap="2">
			<viewContainers:ViewHolder view="{_arrangeViewProperties}" canCollapse="true" changeHeightFromTop="true"/> 
			<viewContainers:ViewHolder id="_arrangeInfoViewHolder" view="{_arrangeInfoView}" changeHeightFromTop="true" changeWidthFromLeft="true" hasCloseButton="true"/>  
		</mx:HBox>
	</mx:VBox> 
	
	<mx:VBox width="100%" height="100%" paddingLeft="5" paddingRight="5" verticalGap="2" includeIn="ModuleGraphState">
		<mx:HBox width="100%" height="100%" horizontalGap="2">
			<viewContainers:ViewHolder view="{_moduleGraph}" hasActiveButton="true"/>
			<viewContainers:ViewHolder view="{_moduleLibrary}" changeWidthFromLeft="true"/>
		</mx:HBox>
		<mx:HBox id="_moduleGraphInfoContainer" width="100%" horizontalGap="2">
			<viewContainers:ViewHolder view="{_moduleProperties}" canCollapse="true" changeHeightFromTop="true"/>
			<viewContainers:ViewHolder id="_moduleGraphInfoViewHolder" view="{_moduleGraphInfoView}" changeHeightFromTop="true" changeWidthFromLeft="true" hasCloseButton="true"/>
		</mx:HBox>
	</mx:VBox>
	
	<mx:VBox width="100%" height="100%" paddingLeft="5" paddingRight="5" verticalGap="2" includeIn="LiveState">
		<viewContainers:ViewHolder view="{_liveView}" height="100%" timelineMode="{TimelineMode.READONLY}" hasActiveButton="true"/>
		<mx:HBox id="_liveInfoContainer" width="100%" horizontalGap="2">
			<viewContainers:ViewHolder view="{_keybindingView}" canCollapse="true" changeHeightFromTop="true"/> 
			<viewContainers:ViewHolder id="_liveInfoViewHolder" view="{_liveInfoView}" changeHeightFromTop="true" changeWidthFromLeft="true" hasCloseButton="true"/>
		</mx:HBox>
	</mx:VBox>

	<fx:Declarations>
		<utils:DimensionSharer view1="{_arrangeViewProperties}" view2="{_arrangeInfoView}" dimension="HEIGHT"/>
		<utils:DimensionSharer view1="{_moduleProperties}" view2="{_moduleGraphInfoView}" dimension="HEIGHT"/>
		<utils:DimensionSharer view1="{_keybindingView}" view2="{_liveInfoView}" dimension="HEIGHT"/>
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import flash.filters.ColorMatrixFilter;
			
			import mx.containers.Canvas;
			import mx.core.Container;
			import mx.core.FlexGlobals;
			import mx.managers.PopUpManager;
			
			import components.controller.serverCommands.UpgradeModules;
			import components.controller.userDataCommands.PollForUpgradableModules;
			import components.controller.userDataCommands.SetColorScheme;
			import components.controller.userDataCommands.SetContrast;
			import components.controller.userDataCommands.SetInstallResult;
			import components.controller.userDataCommands.SetViewMode;
			import components.controller.userDataCommands.ShowInfoView;
			import components.model.Project;
			import components.model.userData.ColorScheme;
			import components.model.userData.ViewMode;
			import components.utils.Trace;
			import components.views.ArrangeView.ArrangeView;
			import components.views.ArrangeViewProperties.ArrangeViewProperties;
			import components.views.BlockLibrary.BlockLibrary;
			import components.views.InfoView.InfoView;
			import components.views.LiveView.KeybindingView;
			import components.views.LiveView.LiveView;
			import components.views.ModuleGraph.ModuleGraph;
			import components.views.ModuleLibrary.ModuleLibrary;
			import components.views.ModuleManager.ModuleManagementInfoGenerator;
			import components.views.ModuleManager.ModuleManager;
			import components.views.ModuleManager.ModuleManagerReport;
			import components.views.ModuleManager.UpgradeDialog;
			import components.views.ModuleProperties.ModuleProperties;
			import components.views.Preferences.Preferences;
			import components.views.Timeline.TimelineMode;
			import components.views.viewContainers.ViewTree;
			
			import flexunit.framework.Assert;
			
			
			public function loadPersistantState( xml:XML ):void
			{
				loadHeight( xml, _arrangeViewProperties, "arrangeViewPropertiesHeight" );
				loadCollapsed( xml, _arrangeViewProperties, "arrangeViewPropertiesCollapsed" );
				loadWidth( xml, _blockLibrary, "blockLibraryWidth" );
				loadHeight( xml, _arrangeInfoView, "arrangeInfoViewHeight" );
				loadWidth( xml, _arrangeInfoView, "arrangeInfoViewWidth", _blockLibrary.width );
				loadHeight( xml, _moduleProperties, "modulePropertiesHeight" );
				loadCollapsed( xml, _moduleProperties, "modulePropertiesCollapsed" );
				loadWidth( xml, _moduleLibrary, "moduleLibraryWidth" );
				loadHeight( xml, _moduleGraphInfoView, "moduleGraphInfoViewHeight" );
				loadWidth( xml, _moduleGraphInfoView, "moduleGraphInfoViewWidth", _moduleLibrary.width );
				loadHeight( xml, _keybindingView, "keybindingViewHeight" );
				loadCollapsed( xml, _keybindingView, "keybindingViewCollapsed" );
				loadWidth( xml, _liveInfoView, "liveInfoViewWidth" );
				loadHeight( xml, _liveInfoView, "liveInfoViewHeight" );

				if( xml.hasOwnProperty( "infoViewShowing" ) )
				{
					model.showInfoView = ( xml.child( "infoViewShowing" ).toString() == "true" );
				}
				
				if( xml.hasOwnProperty( "alwaysUpgrade" ) )
				{
					model.alwaysUpgrade = ( xml.child( "alwaysUpgrade" ).toString() == "true" );
				}				

				if( xml.hasOwnProperty( "arrangeViewPropertiesTab" ) )
				{
					_arrangeViewProperties.selectedTab = xml.child( "arrangeViewPropertiesTab" ).toString();
				}				

				if( xml.hasOwnProperty( "lastModuleDirectory" ) )
				{
					_moduleGraph.lastModuleDirectory = xml.child( "lastModuleDirectory" ).toString();
				}

				if( xml.hasOwnProperty( "lastBlockDirectory" ) )
				{
					_arrangeView.lastBlockDirectory = xml.child( "lastBlockDirectory" ).toString();
				}

				if( xml.hasOwnProperty( "lastTrackDirectory" ) )
				{
					_arrangeView.lastTrackDirectory = xml.child( "lastTrackDirectory" ).toString();
				}
			}


			public function storePersistantState( xml:XML ):void
			{
				xml.appendChild( <arrangeViewPropertiesHeight>{ _arrangeViewProperties.height }</arrangeViewPropertiesHeight> );
				xml.appendChild( <arrangeViewPropertiesCollapsed>{ _arrangeViewProperties.collapsed }</arrangeViewPropertiesCollapsed> );
				xml.appendChild( <blockLibraryWidth>{ _blockLibrary.width }</blockLibraryWidth> );
				xml.appendChild( <arrangeInfoViewHeight>{ _arrangeInfoView.height }</arrangeInfoViewHeight> );
				xml.appendChild( <arrangeInfoViewWidth>{ _arrangeInfoView.width }</arrangeInfoViewWidth> );

				xml.appendChild( <modulePropertiesHeight>{ _moduleProperties.height }</modulePropertiesHeight> );
				xml.appendChild( <modulePropertiesCollapsed>{ _moduleProperties.collapsed }</modulePropertiesCollapsed> );
				xml.appendChild( <moduleLibraryWidth>{ _moduleLibrary.width }</moduleLibraryWidth> );
				xml.appendChild( <moduleGraphInfoViewHeight>{ _moduleGraphInfoView.height }</moduleGraphInfoViewHeight> );
				xml.appendChild( <moduleGraphInfoViewWidth>{ _moduleGraphInfoView.width }</moduleGraphInfoViewWidth> );

				xml.appendChild( <keybindingViewHeight>{ _keybindingView.height }</keybindingViewHeight> );
				xml.appendChild( <keybindingViewCollapsed>{ _keybindingView.collapsed }</keybindingViewCollapsed> );
				xml.appendChild( <liveInfoViewWidth>{ _liveInfoView.width }</liveInfoViewWidth> );
				xml.appendChild( <liveInfoViewHeight>{ _liveInfoView.height }</liveInfoViewHeight> );

				xml.appendChild( <infoViewShowing>{ model.showInfoView }</infoViewShowing> );
				xml.appendChild( <alwaysUpgrade>{ model.alwaysUpgrade }</alwaysUpgrade> );
				
				xml.appendChild( <arrangeViewPropertiesTab>{ _arrangeViewProperties.selectedTab }</arrangeViewPropertiesTab> );
				
				if( _moduleGraph.lastModuleDirectory )
				{
					xml.appendChild( <lastModuleDirectory>{ _moduleGraph.lastModuleDirectory }</lastModuleDirectory> );
				}

				if( _arrangeView.lastBlockDirectory )
				{
					xml.appendChild( <lastBlockDirectory>{ _arrangeView.lastBlockDirectory }</lastBlockDirectory> );
				}
				
				if( _arrangeView.lastTrackDirectory )
				{
					xml.appendChild( <lastTrackDirectory>{ _arrangeView.lastTrackDirectory }</lastTrackDirectory> );
				}
			}


			override public function styleChanged( style:String ):void
			{
				if( !style || style == ColorScheme.STYLENAME )
				{
					var comboTextColor:uint;
					var comboBackgroundColor:uint;
					var comboSelectedBackgroundColor:uint;
					
					var checkBoxTextColor:uint;
					var checkBoxSelectedTextColor:uint;
					var checkBoxBackgroundColor:uint;
					
					switch( getStyle( ColorScheme.STYLENAME ) )
					{
						default:
						case ColorScheme.LIGHT:
							opaqueBackground = 0xf5f5f5;
							_moduleGraphTransitionCanvas.setStyle( "backgroundColor", 0xC0C0C0 );

							comboTextColor = 0xcfcfcf;
							comboBackgroundColor = 0x747474;
							comboSelectedBackgroundColor = 0x848484;

							checkBoxTextColor = 0x747474;
							checkBoxSelectedTextColor = 0x313131;
							checkBoxBackgroundColor = 0xffffff;
							
							break;
							
						case ColorScheme.DARK:
							opaqueBackground = 0x0b0b0b;
							_moduleGraphTransitionCanvas.setStyle( "backgroundColor", 0x404040 );

							comboTextColor = 0x313131;
							comboBackgroundColor = 0x8c8c8c;
							comboSelectedBackgroundColor = 0x7c7c7c;

							checkBoxTextColor = 0x8c8c8c;
							checkBoxSelectedTextColor = 0xcfcfcf;
							checkBoxBackgroundColor = 0x000000;
							
							break;
					}

					var comboBoxStyle:CSSStyleDeclaration = FlexGlobals.topLevelApplication.styleManager.getStyleDeclaration( "mx.controls.ComboBox" );
					if( comboBoxStyle )
					{
						comboBoxStyle.setStyle( "color", comboTextColor );
						comboBoxStyle.setStyle( "textRollOverColor", comboTextColor );
						comboBoxStyle.setStyle( "textSelectedColor", comboTextColor );
						comboBoxStyle.setStyle( "rollOverColor", comboSelectedBackgroundColor );
						comboBoxStyle.setStyle( "selectionColor", comboSelectedBackgroundColor );
						comboBoxStyle.setStyle( "alternatingItemColors", [ comboBackgroundColor, comboBackgroundColor ] ); 					
					}

					var menuStyle:CSSStyleDeclaration = FlexGlobals.topLevelApplication.styleManager.getStyleDeclaration( "mx.controls.Menu" );
					if( menuStyle )
					{
						menuStyle.setStyle( "color", comboTextColor );
						menuStyle.setStyle( "textRollOverColor", comboTextColor );
						menuStyle.setStyle( "textSelectedColor", comboTextColor );
						menuStyle.setStyle( "rollOverColor", comboSelectedBackgroundColor );
						menuStyle.setStyle( "selectionColor", comboSelectedBackgroundColor );
						menuStyle.setStyle( "backgroundColor", comboBackgroundColor ); 					
					}
					
					var checkBoxStyle:CSSStyleDeclaration = FlexGlobals.topLevelApplication.styleManager.getStyleDeclaration( "mx.controls.CheckBox" );
					if( checkBoxStyle )
					{
						checkBoxStyle.setStyle( "color", checkBoxTextColor );
						checkBoxStyle.setStyle( "textRollOverColor", checkBoxSelectedTextColor );
						checkBoxStyle.setStyle( "textSelectedColor", checkBoxSelectedTextColor );
						checkBoxStyle.setStyle( "backgroundColor", checkBoxBackgroundColor ); 					
					}				
					
				}

				if( _isDisplayingPreferences )
				{
					_preferences.onStyleChanged( style );	
				}

				if( _isDisplayingModuleManager )
				{
					_moduleManager.onStyleChanged( style );	
				}

				if( _isDisplayingUpgradeDialog )
				{
					_upgradeDialog.setStyle( style, getStyle( "style" ) );
					_upgradeDialog.onStyleChanged( style );	
				}
				
				_reportView.styleChanged( style );
			}
	
	
			override protected function onAllDataChanged():void 
			{
				endModuleGraphTransition();

				updateState();
				
				updateColorSchemeAndContrast();
				
				updatePreferences();
				updateModuleManager();
				updateUpgradeDialog();
			}


			private function onCreationComplete():void
			{
				addUpdateMethod( SetColorScheme, onColorSchemeChanged );
				addUpdateMethod( SetContrast, onContrastChanged );
				addUpdateMethod( SetViewMode, onViewModeChanged ); 
				addUpdateMethod( ShowInfoView, onInfoViewShow );
				addUpdateMethod( PollForUpgradableModules, onPollForUpgradableModules );
				addUpdateMethod( UpgradeModules, onUpgradeModules );
				addUpdateMethod( SetInstallResult, onInstallResult );
				
				_moduleGraphTransitionTimer.addEventListener( TimerEvent.TIMER, onModuleGraphTransitionTimer );
				_moduleGraphTransitionTimer.addEventListener( TimerEvent.TIMER_COMPLETE, onModuleGraphTransitionComplete );
				
				_moduleGraphTransitionCanvas.alpha = 0.5;
				
				addEventListener( Event.RESIZE, onResize );
			}
			
			
			private function onResize( event:Event ):void
			{
				if( _isDisplayingPreferences )
				{
					_preferences.updateSize();
					mx.managers.PopUpManager.centerPopUp( _preferences );
				}

				if( _isDisplayingModuleManager )
				{
					_moduleManager.updateSize();
					mx.managers.PopUpManager.centerPopUp( _moduleManager );
				}
			}
	
	
			private function onViewModeChanged( command:SetViewMode ):void
			{
				updatePreferences();
				updateModuleManager();
				updateUpgradeDialog();

				var viewMode:ViewMode = model.project.projectUserData.viewMode;
				if( viewMode.mode == ViewMode.ARRANGE )
				{
					if( viewMode.blockPropertiesOpen && currentState.indexOf( "ArrangeState" ) == 0 )
					{
						startModuleGraphTransition();
						return;
					}
					else
					{
						if( !viewMode.blockPropertiesOpen && currentState.indexOf( "ModuleGraphState" ) == 0 )
						{
							updateState();
							startModuleGraphTransition();
							return;
						}
					}
				}
				
				updateState();
			}
			
			
			private function onColorSchemeChanged( command:SetColorScheme ):void
			{
				updateColorSchemeAndContrast();
			}

			
			private function onContrastChanged( command:SetContrast ):void
			{
				updateColorSchemeAndContrast();
			}
			
			
			private function onInfoViewShow( command:ShowInfoView ):void
			{
				updateState();
			}
			
			
			private function onPollForUpgradableModules( command:PollForUpgradableModules ):void
			{
				if( command.foundUpgradableModules )
				{
					_upgradeDialog.objectID = command.searchObjectID;
				}
			}
			
			
			private function onUpgradeModules( command:UpgradeModules ):void
			{
				var infoGenerator:ModuleManagementInfoGenerator = new ModuleManagementInfoGenerator();
				var upgradeReportMarkdown:String = infoGenerator.getUpgradeReport( command );
				_reportView.displayReport( "Module Upgrade Report:", upgradeReportMarkdown, this );
			}
			
			
			private function onInstallResult( command:SetInstallResult ):void
			{
				_reportView.displayReport( "Installation Report:", command.installResult, this );
			}
			
			
			private function updateState():void
			{
				var viewMode:ViewMode = model.project.projectUserData.viewMode;
				
				var infoViewHolder:ViewHolder = null;
				var infoContainer:Container = null;
				
				switch( viewMode.mode )
				{
					case ViewMode.ARRANGE:
						if( viewMode.blockPropertiesOpen )
						{
							currentState = "ModuleGraphState";
							infoViewHolder = _moduleGraphInfoViewHolder;
							infoContainer = _moduleGraphInfoContainer;
						}
						else
						{	
							currentState = "ArrangeState";
							infoViewHolder = _arrangeInfoViewHolder;
							infoContainer = _arrangeInfoContainer;
						}
						break;
	
					case ViewMode.LIVE:
						currentState = "LiveState";
						infoViewHolder = _liveInfoViewHolder;
						infoContainer = _liveInfoContainer;
						break;
						
					default:
						Assert.assertTrue( false );
						break;
				}
				
				Assert.assertTrue( infoViewHolder && infoContainer );
				
				if( model.showInfoView )
				{
					if( infoViewHolder.parent != infoContainer )
					{
						infoContainer.addChild( infoViewHolder );
					}
				}
				else
				{
					if( infoViewHolder.parent == infoContainer )
					{
						infoContainer.removeChild( infoViewHolder );
					}
				}
			}
			
			
			private function updateColorSchemeAndContrast():void
			{
				const contrastMultiplier:Number = 2;
				
				var colorScheme:String = model.project.projectUserData.colorScheme;
				if( parentApplication.getStyle( ColorScheme.STYLENAME ) != colorScheme )
				{
					parentApplication.setStyle( ColorScheme.STYLENAME, colorScheme );
				}

				const filterMatrixInvert:Array = 
					[
						-1, 0, 0, 0, 255,
						0, -1, 0, 0, 255,
						0, 0, -1, 0, 255,
						0, 0, 0, 1, 0 
					];

				
				const filterMatrixIncreaseDark:Array = 
				[
					contrastMultiplier, 0, 0, 0, 0,
					0, contrastMultiplier, 0, 0, 0,
					0, 0, contrastMultiplier, 0, 0,
					0, 0, 0, 1, 0 
				];	

				if( model.project.projectUserData.highContrast )
				{
					switch( colorScheme )
					{
						case ColorScheme.LIGHT:
							
							parentApplication.filters = 
								[ 
									new ColorMatrixFilter( filterMatrixInvert ),
									new ColorMatrixFilter( filterMatrixIncreaseDark ), 
									new ColorMatrixFilter( filterMatrixInvert )
								];
							
							break;

						case ColorScheme.DARK:
							
							parentApplication.filters = 
							[ 
								new ColorMatrixFilter( filterMatrixIncreaseDark ), 
							];
							
							break;
						
						default:
							Assert.assertTrue( false );
							break;
					}
				}
				else
				{
					parentApplication.filters = [];
				}
			}
			
			
			private function updatePreferences():void
			{
     			if( model.project.projectUserData.viewMode.preferencesOpen )
     			{
     				if( !_isDisplayingPreferences )
     				{
						mx.managers.PopUpManager.addPopUp( _preferences, this );
						mx.managers.PopUpManager.centerPopUp( _preferences );
						_preferences.onStyleChanged( null );	
						mx.managers.PopUpManager.bringToFront( _preferences );
						_isDisplayingPreferences = true;
     				}
     			}
     			else
     			{
     				if( _isDisplayingPreferences )
     				{
    					mx.managers.PopUpManager.removePopUp( _preferences );
 	      				_isDisplayingPreferences = false;
 	      			}
     			}
			}

			
			private function updateModuleManager():void
			{
				if( model.project.projectUserData.viewMode.moduleManagerOpen )
				{
					if( !_isDisplayingModuleManager )
					{
						mx.managers.PopUpManager.addPopUp( _moduleManager, this );
						mx.managers.PopUpManager.centerPopUp( _moduleManager );
						_moduleManager.onStyleChanged( null );	
						mx.managers.PopUpManager.bringToFront( _moduleManager );
						_isDisplayingModuleManager = true;
					}
				}
				else
				{
					if( _isDisplayingModuleManager )
					{
						mx.managers.PopUpManager.removePopUp( _moduleManager );
						_isDisplayingModuleManager = false;
					}
				}
			}

			
			private function updateUpgradeDialog():void
			{
				if( model.project.projectUserData.viewMode.upgradeDialogOpen )
				{
					if( !_isDisplayingUpgradeDialog )
					{
						mx.managers.PopUpManager.addPopUp( _upgradeDialog, this );
						_upgradeDialog.onStyleChanged( null );	
						mx.managers.PopUpManager.centerPopUp( _upgradeDialog );
						mx.managers.PopUpManager.bringToFront( _upgradeDialog );
						_isDisplayingUpgradeDialog = true;
					}
				}
				else
				{
					if( _isDisplayingUpgradeDialog )
					{
						mx.managers.PopUpManager.removePopUp( _upgradeDialog );
						_isDisplayingUpgradeDialog = false;
					}
				}
			}
			
			
			private function startModuleGraphTransition():void
			{
				endModuleGraphTransition();
				
				_moduleGraphTransitionRectangle = _arrangeView.getPrimaryBlockRectangle();
				if( !_moduleGraphTransitionRectangle ) 
				{
					Trace.error( "failed to query module graph transition rectangle" );
					_moduleGraphTransitionRectangle = new Rectangle();	//failsafe
				}
				
				var arrangeViewRect:Rectangle = _arrangeView.getRect( this );
				_moduleGraphTransitionRectangle.offset( arrangeViewRect.x, arrangeViewRect.y );

				_moduleGraphTransitionCanvas.visible = false;
				addElement( _moduleGraphTransitionCanvas );
				
				_moduleGraphTransitionTimer.start();
				
				_moduleGraphTransitionActive = true;
				
				doModuleGraphTransition();
			}
			
			
			private function endModuleGraphTransition():void
			{
				if( !_moduleGraphTransitionActive )
				{
					return;
				}
				removeElement( _moduleGraphTransitionCanvas );
				_moduleGraphTransitionCanvas.visible = false;
				
				_moduleGraphTransitionTimer.stop();
				_moduleGraphTransitionTimer.reset();
				
				_moduleGraphTransitionActive = false;
			}


			private function onModuleGraphTransitionTimer( event:TimerEvent ):void
			{
				doModuleGraphTransition();				
			}


			private function doModuleGraphTransition():void
			{
				var transition:Number = _moduleGraphTransitionTimer.currentCount / _moduleGraphTransitionTimer.repeatCount;
				
				//easing function
				transition = 0.5 - Math.cos( transition * Math.PI ) * 0.5;
				
				if( !model.project.projectUserData.viewMode.blockPropertiesOpen )
				{
					transition = 1 - transition;
				}
				
				var transitionOpposite:Number = 1 - transition;
				  
				_moduleGraphTransitionCanvas.x = _moduleGraphTransitionRectangle.x * transitionOpposite;
				_moduleGraphTransitionCanvas.y = _moduleGraphTransitionRectangle.y * transitionOpposite;
				_moduleGraphTransitionCanvas.width = width * transition + _moduleGraphTransitionRectangle.width * transitionOpposite;
				_moduleGraphTransitionCanvas.height = height * transition +  _moduleGraphTransitionRectangle.height * transitionOpposite;
				_moduleGraphTransitionCanvas.visible = true;
			}


			private function onModuleGraphTransitionComplete( event:TimerEvent ):void
			{
				endModuleGraphTransition();
				updateState();
			}
			
			
			private function loadHeight( xml:XML, view:IntegraView, key:String, defaultHeight:Number = -1 ):void
			{
				if( xml.hasOwnProperty( key ) )
				{
					view.height = Number( xml.child( key ).toString() );
				}
				else
				{
					if( defaultHeight >= 0 ) 
					{
						view.height = Math.max( view.minHeight, Math.min( view.maxHeight, defaultHeight ) );;
					}
				}
			}

			
			private function loadWidth( xml:XML, view:IntegraView, key:String, defaultWidth:Number = -1 ):void
			{
				if( xml.hasOwnProperty( key ) )
				{
					view.width = Number( xml.child( key ).toString() );
				}
				else
				{
					if( defaultWidth >= 0 ) 
					{
						view.width = Math.max( view.minWidth, Math.min( view.maxWidth, defaultWidth ) );
					}
				}
			}

			
			private function loadCollapsed( xml:XML, view:IntegraView, key:String ):void
			{
				if( xml.hasOwnProperty( key ) )
				{
					view.collapsed = ( xml.child( key ).toString() == "true" );
				}
			}
			
			
			
			[Bindable] private var _arrangeView:ArrangeView = new ArrangeView;
			[Bindable] private var _arrangeViewProperties:ArrangeViewProperties = new ArrangeViewProperties;
			[Bindable] private var _blockLibrary:BlockLibrary = new BlockLibrary;
			[Bindable] private var _moduleGraph:ModuleGraph = new ModuleGraph;
			[Bindable] private var _moduleLibrary:ModuleLibrary = new ModuleLibrary;
			[Bindable] private var _moduleProperties:ModuleProperties = new ModuleProperties;
			[Bindable] private var _liveView:LiveView = new LiveView;
			[Bindable] private var _keybindingView:KeybindingView = new KeybindingView;
			[Bindable] private var _preferences:Preferences = new Preferences;
			[Bindable] private var _moduleManager:ModuleManager = new ModuleManager;
			[Bindable] private var _upgradeDialog:UpgradeDialog = new UpgradeDialog;
			[Bindable] private var _arrangeInfoView:InfoView = new InfoView( true );
			[Bindable] private var _moduleGraphInfoView:InfoView = new InfoView( true );
			[Bindable] private var _liveInfoView:InfoView = new InfoView( true );

			private var _isDisplayingPreferences:Boolean = false;
			private var _isDisplayingModuleManager:Boolean = false;
			private var _isDisplayingUpgradeDialog:Boolean = false;

			private var _moduleGraphTransitionTimer:Timer = new Timer( _moduleGraphTransitionDelay, _moduleGraphTransitionDuration / _moduleGraphTransitionDelay );
			private var _moduleGraphTransitionCanvas:Canvas = new Canvas;
			private var _moduleGraphTransitionActive:Boolean = false;
			private var _moduleGraphTransitionRectangle:Rectangle = null;

			private const _moduleGraphTransitionDelay:Number = 50;
			private const _moduleGraphTransitionDuration:Number = 300;
			
			private var _reportView:ModuleManagerReport = new ModuleManagerReport;
			
		]]>
	</fx:Script>					

</IntegraView>
