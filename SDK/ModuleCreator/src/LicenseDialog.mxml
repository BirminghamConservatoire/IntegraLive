<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:fx="http://ns.adobe.com/mxml/2009" 
		   xmlns:mx="library://ns.adobe.com/flex/mx" 
		   creationComplete="onCreationComplete()"
		   backgroundColor="#ffffff"
		   borderStyle="solid" borderThickness="4" borderColor="#c0c0c0">

	<mx:VBox left="0" top="0" right="0" bottom = "0">
		<mx:HTML id="_html" width="100%" height="100%"/>
		<mx:HBox width="100%" horizontalGap="20" horizontalAlign="center" paddingTop="10" paddingBottom="10">
			<mx:Button id="_acceptButton" click="onAccept()"/>		
			<mx:Button id="_declineButton" click="onDecline()"/>		
		</mx:HBox>
	</mx:VBox>
	
	
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.cstrahan.Showdown;
			
			import mx.managers.PopUpManager;
			
			
			public function set includeAcceptanceButton( includeAcceptanceButton:Boolean ):void
			{
				if( includeAcceptanceButton )
				{
					_acceptButton.label = "Accept";
					_declineButton.label = "Decline";
				}
				else
				{
					_acceptButton.label = "Close";
					_declineButton.parent.removeChild( _declineButton );
				}
			}
			
			
			public function get accepted():Boolean
			{
				return _accepted;
			}
			
			
			private function onCreationComplete():void
			{
				var licenseAgreementFile:File = File.applicationDirectory.resolvePath( _licenseAgreementFileName );
				if( !licenseAgreementFile.exists )
				{
					trace( "Can't find license agreement file " + licenseAgreementFile.nativePath );
					return;
				}
				
				var fileStream:FileStream = new FileStream();
				fileStream.open( licenseAgreementFile, FileMode.READ );
				var markdown:String = fileStream.readUTFBytes( licenseAgreementFile.size );
				fileStream.close();
				var body:String = Showdown.makeHtml( markdown )
					
				var licenseAgreementCSSFile:File = File.applicationDirectory.resolvePath( _licenseAgreementCSSFileName );
				if( !licenseAgreementCSSFile.exists )
				{
					trace( "Can't find license agreement CSS file " + licenseAgreementCSSFile.nativePath );
					return;
				}
				
				fileStream = new FileStream();
				fileStream.open( licenseAgreementCSSFile, FileMode.READ );
				var stylesheet:String = fileStream.readUTFBytes( licenseAgreementCSSFile.size );
				fileStream.close();
					
				var html:String = "<head><style>" + stylesheet + "</style></head><body>" + body + "</body"; 
				
				_html.htmlText = html;
			}
			
			
			private function onAccept():void
			{
				_accepted = true;
				PopUpManager.removePopUp( this );
			}

			
			private function onDecline():void
			{
				_accepted = false;
				PopUpManager.removePopUp( this );
			}
			
			
			private var _accepted:Boolean = false;


			private static const _licenseAgreementFileName:String = "assets/agreement.md";
			private static const _licenseAgreementCSSFileName:String = "assets/agreement.css";
			
		]]>
	</fx:Script>			

			
</mx:Canvas>
